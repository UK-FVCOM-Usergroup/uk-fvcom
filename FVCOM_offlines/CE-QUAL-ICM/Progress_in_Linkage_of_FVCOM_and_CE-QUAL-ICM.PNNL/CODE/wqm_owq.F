!=========================================================================
!
!=========================================================================
   SUBROUTINE LGHT_ATTN
   USE WQM
   USE ALL_VARS
   
   IMPLICIT NONE
   REAL    :: ITHTAA
   INTEGER :: I,K
   REAL,PARAMETER :: TWOPI = 6.2832
!vjp 9/19/2005 added declarations
   REAL :: DOY, HOUR, SOLDATE, DECL, TAU, CTHTAA, THTAW, AVREFCOS
   REAL :: TOTCHL, TOTPOC, TSS, TOTDOC, OWQ, IATTOP, OPTDEPTH

!vjp  10/11/04 initialize arrays OPZ, G1, and G2

   OPZ(1:9) = (/ 0.0, 0.01, 0.025, 0.033, 0.05, 0.1, 0.2, 0.286, 0.4 /)
   G1(1:9) =  (/ 0.425, 0.425, 0.364, 0.3383, 0.2996, 0.2175,           &
                 0.1232, 0.073167, 0.01546 /)
   G2(1:9) =  (/ 0.19, 0.19, 0.1396, 0.1187, 0.0875, 0.02288,           &
                -0.048, -0.08367, -0.12386 /)
                
! CONVERT MODEL JULIAN DAY TO DAY AND HOUR

   DOY = AMOD(JDAY,365.)
   HOUR = 24.*(DOY-AINT(DOY))
   DOY = AINT(DOY)+DOFFSET

! COMPUTE COSIN OF IN-WATER SOLAR ANGLE

! SOLAR DATE
   SOLDATE = TWOPI*DOY/365.

! DECLINATION
   DECL = TWOPI*(0.39637-(22.9133*COS(SOLDATE))+(4.02543*SIN(SOLDATE))-  &
         (0.3872*COS(2.*SOLDATE))+(0.052*SIN(2*SOLDATE)))/360.

! TIME OF DAY
   TAU = TWOPI*HOUR/24.

! COSIN OF THETA IN AIR
   CTHTAA=((SIN(LAT))*(SIN(DECL)))-((COS(LAT))*(COS(DECL))*(COS(TAU)))

! INCIDENCE OF THETA IN AIR
   ITHTAA = ACOS(CTHTAA)

! THETA IN WATER
   THTAW=ASIN(SIN(ITHTAA)/1.33)

! REFLECTANCE
   IF (ITHTAA > TWOPI/4.) THEN
     SREFLECT = 100.
   ELSE
     SREFLECT = EXP(0.6148+ITHTAA**3)
   END IF

! SCATTERING FUNCTION
   AVREFCOS = COS(THTAW)
   GMUZERO(1) = 0.198
   DO I=2,9
     GMUZERO(I) = AVREFCOS*G1(I)-G2(I)
   END DO

! EVALUATE ATTENUATION FOR EACH SURFACE CELL

   DO I=1,MLOC
     TOTCHL = (B1(I,1)*CHLCMN1 + B2(I,1)*CHLCMN2 + B3(I,1)*CHLCMN3)*TCHL2CHL

! EMPIRICAL FUNCTIONS FOR COLOR AND TURBIDITY

     TOTPOC   = B1(I,1)+B2(I,1)+B3(I,1)+SZ(I,1)+LZ(I,1)+LPOC(I,1)+RPOC(I,1)
     TSS      = MAX(SSI(I,1) + 2.5*TOTPOC, 1.0) 
     TURB(I,1)  = MIN(0.4*TSS,12.)
     TOTDOC   = MIN(35.,LDOC(I,1)+RDOC(I,1))           !  used to be 25
     COLOR(I,1) = MAX(12.2*EXP(0.14*TOTDOC), 10.)
        
     KESS(I,1) = OWQ(COLOR(I,1),TOTCHL,TURB(I,1),DEPTHKE,AVREFCOS)  
      
! ASSIGN SURFACE ATTENUATION TO SUB-SURFACE CELLS

     DO K=2,KBM1
       KESS(I,K) = KESS(I,1)
     END DO
   END DO
      
! *** Compute irradiance in each cell 

   DO I=1,MLOC
     IATTOP = I0*(1.-SREFLECT/100.)
     OPTDEPTH = KESS(I,1)*D(I)*DZ(1)
     IATBOT(I,1) = IATTOP*EXP(-OPTDEPTH)
     IAVG(I,1) = (IATTOP-IATBOT(I,1))/OPTDEPTH

     DO K=2,KBM1
       IATTOP = IATBOT(I,K-1)
       OPTDEPTH = KESS(I,K)*D(I)*DZ(K)
       IATBOT(I,K) = IATTOP*EXP(-OPTDEPTH)
       IAVG(I,K) = (IATTOP-IATBOT(I,K))/OPTDEPTH
     END DO 
   END DO
      Print*, 'IAVG at node 25 surf', IAVG(25,1)
   RETURN
   END SUBROUTINE LGHT_ATTN
!=======================================================================
!
!=======================================================================
   REAL FUNCTION OWQ(COLORS,CHL,TURBS,DEPTH,AVREFCOS)
   USE WQM
   USE ALL_VARS

   IMPLICIT NONE
   REAL,DIMENSION(100) :: ABWAT, ABCDOM, ABCHL, ABNAPM,      &
                          ABTOT, SCAT, KDLAMB, EZ         
   REAL :: AG440, APHI676, AP440, B440
   REAL :: COLORS,CHL,TURBS,AVREFCOS,PLR,DEPTH,REFSCAT,EZINT
   INTEGER :: I,J

   AG440   = GSTAR440*COLORS
   APHI676 = PHISTAR676*CHL
   AP440   = PSTARINT*(TURBS**PSTAR440)*(COLORS**PSTARCOLOR)
   B440    = TURBS*BNSTARNTU + CHL*BNSTARCHL

! COMPUTE ABSORPTION AND SCATTTERING FOR EACH WAVELENGTH

   DO I=1,NWAVEL
     ABWAT(I)  = ALAMB(I)
     ABCDOM(I) = GLAMB(I)*AG440
     ABCHL(I)  = PHILAMB(I)*APHI676
     ABNAPM(I) = PLAMB(I)*AP440
     ABTOT(I)  = ABWAT(I)+ABCDOM(I)+ABCHL(I)+ABNAPM(I)
     SCAT(I)   = BLAMB(I)*B440
   END DO

! COMPUTE ATTENUATION FOR EACH WAVELENGTH

   DO I=1,NWAVEL

! FIRST ESTIMATE ATTENUATION BASED ON DEPTH TO 1% LIGHT PENETRATION
     KDLAMB(I) = SQRT(ABTOT(I)*ABTOT(I)+GMUZERO(2)*ABTOT(I)*SCAT(I))   &
                 /AVREFCOS

! PERCENT LIGHT REMAINING
     PLR = EXP(-KDLAMB(I)*DEPTH)

! USE LOOKUP TABLE TO REFINE ESTIMATE OF SCATTERING FUNCTION BASED ON 
! PERCENT LIGHT REMAINING

     REFSCAT = GMUZERO(9)
     DO J=1,8
       IF (PLR >= OPZ(J) .AND. PLR < OPZ(J+1)) THEN
         REFSCAT = GMUZERO(J)
         GO TO 1
       END IF
     END DO
       
1    CONTINUE
        
! RE-EVALUATE ATTENUATION BASED ON REFINED SCATERING FUNCTION
     KDLAMB(I) = SQRT(ABTOT(I)*ABTOT(I)+REFSCAT*ABTOT(I)*SCAT(I))/AVREFCOS 

! COMPUTE REMAINING SPECTRAL IRRADIANCE
     EZ(I) = EZERO(I)*EXP(-KDLAMB(I)*DEPTH)
  
   END DO

! INTEGRATE PAR REMAINING AT THE REFERENCE DEPTH
   SUM=0
   DO I=2,NWAVEL-1
     SUM=SUM+EZ(I)
   END DO
   EZINT=2.5*(EZ(1)+EZ(NWAVEL))+5.*SUM

! COMPUTE DIFFUSE ATTENUATION COEFFICIENT
   OWQ = -ALOG(EZINT/EZEROINT)/DEPTH

   RETURN
   END FUNCTION OWQ
!=======================================================================
!
!=======================================================================

